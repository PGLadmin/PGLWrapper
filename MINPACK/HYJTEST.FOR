C
C  DRIVER FOR HYBRJ1 EXAMPLE.
C  THIS ROUTINE MUST BE LINKED WITH HYBRJ1.FOR AND HYBRSUBS.FOR
C  LAST CHECKED - 5/93 JRE
C
CCC      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER J,N,INFO,LWA,NWRITE,LDFJAC
      DOUBLE PRECISION TOL,FNORM
      DOUBLE PRECISION X(9),FVEC(9),WA(180),FJAC(9,9)
      DOUBLE PRECISION ENORM,DPMPAR
      EXTERNAL FCN
C
C  LOGICAL OUTPUT UNIT IS ASSUMED TO BE NUMBER 6.
C
      DATA NWRITE/6/
C
      N=9
C
C  THE FOLLOWING STARTING VALUES PROVIDE A ROUGH SOLUTION.
C
      DO 10 J=1,9
        X(J)=-1.D0
10    CONTINUE
C
      LWA=180
      LDFJAC=9
C
C  SET TOL TO THE SQUARE ROOT OF THE MACHINE PRECISION.
C  UNLESS HIGH PRECISION SOLUTIONS ARE REQUIRED,
C  THIS IS THE RECOMMENDED SETTING.
C
       TOL=SQRT(DPMPAR(1))
C      TOL=1.0D-4
C
      CALL HYBRJ1(FCN,N,X,FVEC,FJAC,LDFJAC,TOL,INFO,WA,LWA)
      FNORM=ENORM(N,FVEC)
      WRITE(NWRITE,1000)FNORM,INFO,(X(J),J=1,N)
      STOP
1000  FORMAT(5X,31H FINAL L2 NORM OF THE RESIDUALS,D15.7//
     &       5X,15H EXIT PARAMETER,16X,I10//
     &       5X,27H FINAL APPROXIMATE SOLUTION//(5X,3D15.7))
      END
      SUBROUTINE  FCN(N,X,FVEC,FJAC,LDFJAC,IFLAG)
CCC      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER N,IFLAG,LDFJAC 
      DOUBLE PRECISION X(N),FVEC(N),FJAC(LDFJAC,N)
C
C  SUBROUTINE FCN FOR HYBRD1 EXAMPLE.
C
      INTEGER K,J
      DOUBLE PRECISION ONE,TEMP,TEMP1,TEMP2,THREE,TWO,ZERO,FOUR
      DATA ZERO,ONE,TWO,THREE,FOUR/0.D0,1.D0,2.D0,3.D0,4.D0/
C
      IF(IFLAG.EQ.1)THEN
      DO 10 K=1,N
      WRITE(*,*)'EVALUATING FVEC'
        TEMP=(THREE-TWO*X(K))*X(K)
        TEMP1=ZERO
        IF(K.NE.1)TEMP1=X(K-1)
        TEMP2=ZERO
        IF(K.NE.N)TEMP2=X(K+1)
        FVEC(K)=TEMP-TEMP1-TWO*TEMP2+ONE
        WRITE(6,*)K,X(K),FVEC(K)
10    CONTINUE
      ELSE IF(IFLAG.EQ.2)THEN
      WRITE(*,*)'EVALUATING FJAC'
      DO 40 K=1,N
        DO 30 J=1,N
          FJAC(K,J)=ZERO
30      CONTINUE
        FJAC(K,K)=THREE-FOUR*X(K)
        IF(K.NE.1)FJAC(K,K-1)=-ONE
        IF(K.NE.N)FJAC(K,K+1)=-TWO
40    CONTINUE
      END IF
      RETURN
      END
