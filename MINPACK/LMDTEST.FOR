C
C  DRIVER FOR LMDIF EXAMPLE.
C  THE ANSWERS ARE: 0.0824,1.1330,2.3437 => FNORM=.0906
C
      INTEGER J,M,N,INFO,LWA,NWRITE
      INTEGER IWA(3)
      DOUBLE PRECISION TOL,FNORM
      DOUBLE PRECISION X(3),FVEC(15),WA(75)
      DOUBLE PRECISION ENORM,DPMPAR
      EXTERNAL FCN
C
C  LOGICAL OUTPUT UNIT IS ASSUMED TO BE 6.
C
      DATA NWRITE /6/
C
      M=15
      N=3
C  
C  THE FOLLOWING STARTING VALUES PROVIDE A ROUGH FIT.
C
      X(1)=1.0
      X(2)=1.0
      X(3)=1.0
C
      LWA=75
C
C  SET TOL TO THE SQUARE ROOT OF THE MACHINE PRECISION.
C  UNLESS HIGH PRECISION SOLUTIONS ARE REQUIRED, 
C  THIS IS THE RECOMMENDED SETTING.
C
      TOL=SQRT(DPMPAR(1))
C
      CALL LMDIF1(FCN,M,N,X,FVEC,TOL,INFO,IWA,WA,LWA)
      FNORM=ENORM(M,FVEC)
      WRITE(NWRITE,1000) FNORM,INFO,(X(J),J=1,N)
      STOP
1000  FORMAT(5X,31H FINAL L2 NORM OF THE RESIDUALS,D15.7 //
     &       5X,15H EXIT PARAMETER,16X,I10 //
     &       5X,27H FINAL APPROXIMATE SOLUTION // 5X,3D15.7)
C
C  LAST CARD OF DRIVER FOR LMDIF1 EXAMPLE.
      END
      SUBROUTINE FCN(M,N,X,FVEC,IFLAG)
      INTEGER M,N,IFLAG
      DOUBLE PRECISION X(N),FVEC(M)
C
C  SUBROUTINE FCN FOR LMDIF1 EXAMPLE.
C  THE ANSWERS ARE: 0.0824,1.1330,2.3437 => FNORM=.0906
C
      INTEGER I
      DOUBLE PRECISION TMP1,TMP2,TMP3
      DOUBLE PRECISION Y(15)
      DATA Y/1.4D-1,1.8D-1,2.2D-1,2.5D-1,2.9D-1,3.2D-1,3.5D-1,3.9D-1,
     &       3.7D-1,5.8D-1,7.3D-1,9.6D-1,1.34D0,2.1D0,4.39D0/
      DO 10 I=1,15
        TMP1=I
        TMP2=16-I
        TMP3=TMP1
        IF(I.GT.8)TMP3=TMP2
        FVEC(I)=Y(I)-(X(1)+TMP1/(X(2)*TMP2+X(3)*TMP3))
10    CONTINUE
      RETURN
C
C  LAST CARD OF SUBROUTINE FCN.
C
      END
